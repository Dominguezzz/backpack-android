apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka-android'

group='com.github.Skyscanner.backpack-android'

repositories {
  mavenCentral()
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 1
    versionName rootProject.ext.version

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

  }
}

dependencies {
  implementation "com.android.support:support-annotations:$rootProject.ext.supportLibVersion"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibVersion"
  androidTestImplementation 'com.android.support.test:runner:1.0.1'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
  testImplementation 'junit:junit:4.12'
}

ext.modules = rootProject.subprojects
  .findAll { it.project.name != "Backpack" && it.project.name != "app" }

dokka {
  moduleName = "$rootProject.name"
  outputFormat = 'html'
  outputDirectory = "$rootProject.buildDir/docs"
  reportUndocumented = false

  if (project.hasProperty("dokkaOutput")) {
    outputDirectory = dokkaOutput
  }

  sourceDirs += files(
    modules
      .findAll { it.name != project.name }
      .collect { new File(it.projectDir, "/src/main/java") }
  )

  modules.each { p ->
    linkMapping {
      dir = "${p.projectDir}/src/main/java"
      url = "https://github.com/Skyscanner/backpack-android/tree/master/${p.name}/src/main/java"
      suffix = "#L"
    }
  }
}

